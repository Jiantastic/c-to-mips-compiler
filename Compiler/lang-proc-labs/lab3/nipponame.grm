(* 
as in BNF we have production rules with terminal and non-terminal symbols 
terminal symbols begin with a lowercase letter or are enclosed in double quotes
non-terminal symbols begin with an uppercase letter
*)

I ::= "title:    Japanese names\n"
    ^ "author:   Manta, Pan\n"
    ^ "language: ej\n"
    ^ "topic:    misc\n"
    ^ "status:   refinable\n"
    ^ "audience: anybody\n"
    ^ "created:  03/09/2003"
;

(* the non-terminal I is only for documentation purposes *)

S ::= \FirstName \LastName
;

(* the non terminal S is the grammar starting point *)
(* the \ just means that what comes afterwards will be capitalized *)

FirstName ::= Pre1 ^ Post1
;

(* 
the caret ^ symbol means that
the string generated by non-terminating symbol Pre1 
and the string generated by non-terminating symbol Pre2
will be glued together and not separated by a space
*)

LastName ::= Pre ^ [Mid^] Post
;

(* square brackets mean that the symbol may or may not be included (50% of chance) *)

Pre ::= Pre2 | Mid ;
Post ::= Mid | Post2 ;

(* pipe | means alternative as in BNF *)

Pre1 ::= e | gen | jun | ku | ken | ko | kyo | ryo | ryu | sei | sen | shin | sho | shu | shun |  yo | yu | aki | hide | hiro | masa | nobu | shige | taka | tomo | toshi | yasu | yoshi
;

Post1 ::= ichi | z^(o | u) | ji | kazu
;

Mid ::= yoshi | tada | ie | tsuna | yasu | yori | mochi | taka | kane | tomo | nobu | naga
      |  katsu | toki | masa | mitsu | hisa | hide | toshi | sada | kuni | aki | shige | nori
      |  mune | ki | ita | (h | b) ^ashi | mori | mizu | moto | naka | uchi | yama | oka
      |  saka | no | ike | kawa | tani | sawa | hata | ta | shima | mura | saki
;

Pre2 ::= sane | fuka | asa | mae | yoko | nishi | kita | higashi | minami | au | aka | kuro
      |  kiyo | iwa | ishi | matsu | sugi | take | yone
;

Post2 ::= uji | mori | tsugu | emon | ji | zo | suke | (h | b) ^ ayashi | ue | kami | shita
       |  shimo | zawa | numa | bata | da | jima | +++++++++++++++++++zaki
;

(* preceding a symbol with + symbols increases the chance it will be chosen in the random generation *)
